buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath 'com.neenbedankt.gradle.plugins:android-apt:1.8'
    }
}

plugins {
    id "com.jfrog.bintray" version "1.7"
}

apply plugin: 'com.android.library'
apply plugin: 'maven-publish'
apply plugin: 'com.neenbedankt.android-apt'

group 'net.kr9ly'
version '0.8.0'

android {
    compileSdkVersion 24
    buildToolsVersion "24.0.0"

    defaultConfig {
        minSdkVersion 14
        targetSdkVersion 24
        versionCode 1
        versionName "1.0"
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
}


def releaseJarName = "bright-fw-${version}.jar"

task unpackJar(type: Copy, dependsOn: assemble) {
    from zipTree("${project.buildDir}/intermediates/bundles/release/classes.jar")
    into "${project.buildDir}/tmp/jar"
}

task packJar(type: Zip, dependsOn: unpackJar) {
    from fileTree("${project.buildDir}/tmp/jar")
    exclude '**/BuildConfig.class'
    archiveName releaseJarName
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from android.sourceSets.main.java.srcDirs
}

task javadoc(type: Javadoc) {
    source = android.sourceSets.main.java.srcDirs
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

bintray {
    user = 'kr9ly'
    key = setting_bintray_apikey
    publications = ['brightfw']
    pkg {
        repo = 'maven'
        name = 'bright-fw'
        userOrg = 'kr9ly'
        licenses = ['Apache-2.0']
        vcsUrl = 'https://github.com/kr9ly/bright-fw.git'
    }
}

publishing {
    publications {
        brightfw(MavenPublication) {
            groupId group
            artifactId 'bright-fw'
            artifact source: file("${project.buildDir}/distributions/${releaseJarName}"), builtBy: packJar
            artifact sourcesJar
            artifact javadocJar
        }
    }

    repositories {
        maven {
            url file("${project.buildDir}/.m2/repository")
        }
    }
}


dependencies {
    testCompile 'junit:junit:4.12'
    provided 'com.android.support:support-v4:24.2.1'
    provided 'com.android.support:appcompat-v7:24.2.1'
    provided 'com.google.dagger:dagger:2.5'
    provided 'io.reactivex:rxjava:1.1.6'
    provided 'net.kr9ly:rx-state-machine:1.0.0'
    apt 'com.google.dagger:dagger-compiler:2.5'
}
